apply plugin: 'java'
apply plugin: 'idea'
apply from: "tool.gradle"

//バージョンの記述です。設定すると、Jarの名前が、[teamName]-[version].jarになります。
//version = 1.0.0

repositories {
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile fileTree(dir: 'library', include: '*.jar')
    compile fileTree(dir: 'library/util', include: '*.jar')
    compile fileTree(dir: 'library/rescue/core', include: '*.jar')
    compile fileTree(dir: 'library/rescue/util', include: '*.jar')
}

sourceSets {
    main.java.srcDirs = ['src/java']
}

def defaultName = name

jar {
    destinationDir = new File(projectDir, 'tactics')
    baseName = 'default'.equalsIgnoreCase(teamName) ? defaultName : teamName;
    manifest.attributes('Team-Class': teamClassName)
}

[compileJava, compileTestJava].each {
    it.options.encoding = 'UTF-8'
    it.options.compilerArgs += ['-source', '1.8', '-target', '1.8']
}
javadoc.options.encoding = 'UTF-8'

task start (type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = "adk.Main"
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
    jvmArgs '-Xms512m', '-Xmx4g'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}